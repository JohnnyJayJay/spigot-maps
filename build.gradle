plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '6.0.0'
    id 'com.jfrog.bintray' version '1.8.4'
}

group 'com.github.johnnyjayjay'
version '2.0'

sourceCompatibility = 1.8

repositories {
    maven {
        url "https://hub.spigotmc.org/nexus/content/repositories/snapshots"
    }
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
    mavenCentral()
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:1.16.1-R0.1-SNAPSHOT"
    api "com.madgag:animated-gif-lib:1.4"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier "sources"
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

shadowJar.classifier "withDependencies"

artifacts {
    archives sourcesJar
    archives javadocJar
    archives shadowJar
}

bintray {
    user = System.getenv("BINTRAY_USER")
    key = System.getenv("BINTRAY_KEY")
    publications = ["SpigotMaps"]
    pkg {
        version {
            name = project.version
            released = new Date()
        }
        repo = "spigot-maps"
        name = "spigot-maps"
        vcsUrl = "https://github.com/johnnyjayjay/spigot-maps.git"
    }
    publish = true
}

def pomConfig = {
    licenses {
        license {
            name "GNU Lesser General Public License, Version 3"
            url "https://www.gnu.org/licenses/lgpl-3.0.html"
        }
    }
    developers {
        developer {
            id "johnnyjayjay"
            email "johnnyjayjay02@gmail.com"
        }
    }
    scm {
        url "https://github.com/johnnyjayjay/spigot-maps"
    }
}

publishing {
    publications {
        SpigotMaps(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId "com.github.johnnyjayjay"
            artifactId "spigot-maps"
            version version.toString()
            pom.withXml {
                def root = asNode()
                root.appendNode("name", "spigot-maps")
                root.appendNode("description", "A library to simplify the use of customised maps in Spigot")
                root.appendNode("url", "https://github.com/johnnyjayjay/spigot-maps/")
                root.children().last() + pomConfig
            }
        }
    }
}
