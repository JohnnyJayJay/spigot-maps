plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '6.0.0'
}

group 'com.github.johnnyjayjay'
version '2.1.2'

sourceCompatibility = 1.8

repositories {
    maven {
        url "https://hub.spigotmc.org/nexus/content/repositories/snapshots"
    }
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
    mavenCentral()
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:1.16.1-R0.1-SNAPSHOT"
    api "com.madgag:animated-gif-lib:1.4"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier "sources"
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

shadowJar.classifier "withDependencies"

artifacts {
    archives sourcesJar
    archives javadocJar
    archives shadowJar
}

publishing {
    repositories {
        maven {
            def repo = "https://repo.devcord.club"
            def releaseRepo = "$repo/releases"
            def snapshotRepo = "$repo/snapshots"
            url = uri(version.toString().endsWith("SNAPSHOT") ? snapshotRepo : releaseRepo)

            credentials {
                username devcordUsername
                password devcordPassword
            }
        }
    }

    publications {
        maven(MavenPublication) {
            artifact(javadocJar)
            artifact(sourcesJar)
            from components.java
        }
    }
}